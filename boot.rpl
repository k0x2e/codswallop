( CODSWALLOP RPL, a zen garden
  #####################################################
  Bootstrap )

(This is the file the interpreter is hard-coded to load.
 It tries to load files from a list in modules.rpl in sequence,
 then either runs a file from the commandline or starts the REPL.
 The first part of it is not dependent upon builtins, which is why
 it's funny lookin'.  It also includes very basic single stepping
 and error reporting, though errors often aren't very meaningful
 until later in the boot sequence.)

(-1st, we need some semblance of error handling.)
#0 I*.dup I*.dup 'DEDCONT? I*.sto 'ISDED? I*.sto 'BREAKSST? I*.sto

(Very rudimentary exception handler obeying DEDCONT and crashing out
 if error continuation is off.)
'::
  DEDCONT?
  ':: #1 'ISDED? I*.sto ;
  ':: #0 'ISDED? I*.sto
    I*.errmsg I*.swap " said: " I*.+str I*.swap I*.+str I*.disp
    I*.clrrun ;
  I*.ifte
  ;
'EXCEPT I*.sto

(Zeroth, we hang onto our commandline arguments.)
'ARGS I*.sto

(First, we try to open modules.rpl.)
#1 'DEDCONT? I*.sto BASDIR "modules.rpl" I*.+str I*.dsk>

(Did it work?)
ISDED?
':: `I*.drop "The lack of a modules.rpl suggests you may not have the best time" I*.disp ;
':: (And is it actually a list?)
  `I*.dup `I*.type Types.List I*.==
  ':: #-1
    ':: (SST) idx #1 `I*.+int `I*.dup 'idx `I*.sto modules `I*.len `I*.<
      ':: (Fetch a list entry and try to load it if it's a string.)
        modules idx `I*.get `I*.dup `I*.type Types.String `I*.==
        ':: 
          BASDIR `I*.swap `I*.+str ".rpl" `I*.+str `I*.dsk> ISDED?
          ':: "Module " modules idx `I*.get `I*.+str " is unhappy" 
            `I*.+str `I*.disp "" `I*.disp #0 'ISDED? `I*.sto ;
          `I*.ift ;
        ':: `I*.drop ;
        `I*.ifte ;
      ':: `I*.bail ;
      `I*.ifte `I*.self `I*.eval ;
    { idx modules } `I*.local
    (This comment keeps us from inadvertently hanging onto those locals.) ;
  ':: "You may have a modules.rpl but it's not a great one" `I*.disp ; 
  `I*.ifte ;
`I*.ifte

(By the time we get here, we should be ready to go with builtins, and if
 we aren't, it's probably best we fail here anyhow.)

(Turn error reporting back on.)
#0 'DEDCONT? STO

(It's really easy to crash the interpreter if we leave all the internals
 lying around.  But it's fun to play with, so instead we'll quote the
 internals directory to keep it out of easy reach.)
I* QUOTE 'I* STO

(And hand off to a commandline program or to the user.)

ARGS LEN
':: ARGS DSK> ;
':: 'REPL EXISTS
  ':: VERSION DUP ANSI.settitle DISP REPL ;
  ':: "Successfully failed to find REPL" DISP ;
  IFTE ;
IFTE
